SELECT DISTINCT ?occupationLabel ?person ?personLabel ?genderLabel ?birthDate ?birthPlaceLabel ?heightLabel ?weightLabel ?teamLabel ?positionLabel ?schoolLabel ?awardLabel
WHERE {
  # 日本国籍の人物
  ?person wdt:P27 wd:Q17. 
  
  # 画像を持っていること
  ?person wdt:P18 ?image.
  
  # 対象となる職業
  VALUES ?occupation {
    wd:Q4144610  # アルペンスキー選手
    wd:Q12299841  # クリケット選手
    wd:Q13382608  # クロスカントリースキー選手
    wd:Q16278103  # スカッシュ選手
    wd:Q13382605  # ノルディック複合選手
    wd:Q13388442  # スケルトン選手
    wd:Q476246  # セーリング選手
    wd:Q13382533  # テコンドー選手
    wd:Q16029547  # バイアスロン選手
    wd:Q17361156  # ビーチバレーボール選手
    wd:Q18515558  # フットサル選手
    wd:Q18617021  # フリースタイルスキー選手
    wd:Q767703  # ブレイキン選手
    wd:Q10843263  # フィールドホッケー選手
    wd:Q273285  # リュージュ選手
    wd:Q13382576  # ローイング選手
    wd:Q16004431  # 飛込選手
    wd:Q2730732  # 馬術選手
       
  }
  ?person wdt:P106 ?occupation.

  # 性別
  OPTIONAL { ?person wdt:P21 ?gender. }

  # 生年月日
  OPTIONAL { ?person wdt:P569 ?birthDate. }

  # 出身地（出生地）
  OPTIONAL { ?person wdt:P19 ?birthPlace. }
  
  # 身長
  OPTIONAL { ?person wdt:P2048 ?height. }
  
  # 体重
  OPTIONAL { ?person wdt:P2067 ?weight. }
  
  # 所属チーム
  OPTIONAL { ?person wdt:P54 ?team. }
  
   # ポジション
  OPTIONAL { ?person wdt:P413 ?position. }
  
  # 出身校
  OPTIONAL { ?person wdt:P69 ?school. }
  
  # 受賞
  OPTIONAL { ?person wdt:P166 ?award. }

  # ラベルを日本語優先で取得
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "ja,en".
  }
}

