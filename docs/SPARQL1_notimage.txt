SELECT DISTINCT ?occupationLabel ?person ?personLabel ?genderLabel ?birthDate ?birthPlaceLabel ?heightLabel ?weightLabel ?teamLabel ?positionLabel ?schoolLabel ?awardLabel
WHERE {
  # 日本国籍の人物
  ?person wdt:P27 wd:Q17.

  # 画像を「持っていない」人物
  FILTER NOT EXISTS { ?person wdt:P18 ?image. } 
  
  # 対象となる職業
  VALUES ?occupation {
    wd:Q937857    # サッカー選手
    wd:Q11336312 # プロ野球選手
    wd:Q10871364  # 野球選手
    wd:Q3665646  # バスケットボール選手
    wd:Q2727289  # 力士
    wd:Q11774891  # アイスホッケー選手
    wd:Q13382355  # アーチェリー選手
    wd:Q14089670  # ラグビーユニオン選手
    wd:Q18715859  # アーティスティックスイミング選手
    wd:Q13381572  # 器械体操選手
    wd:Q13381376  # 重量挙げ選手
    wd:Q17516936  # カーリング選手
    wd:Q13382566  # カヌー選手
    wd:Q11303721  # ゴルフ選手
    wd:Q13561328  # サーフィン選手
    wd:Q18200514  # ショートトラック選手
    wd:Q13382603  # スキージャンプ選手
    wd:Q17502714  # スケートボード選手
    wd:Q10843402  # 競泳選手
    wd:Q15709642  # スノーボード選手
    wd:Q10866633  # スピードスケート選手
    wd:Q3951423  # スポーツクライミング選手
    wd:Q10833314  # テニス選手
    wd:Q10980  # トライアスロン選手
    wd:Q23892384  # トランポリン競技選手
    wd:Q13141064  # バドミントン選手
    wd:Q15117302  # バレーボール選手
    wd:Q12840545  # ハンドボール選手
    wd:Q13219587  # フィギュアスケート選手
    wd:Q13381863  # フェンシング選手
    wd:Q11338576  # ボクシング選手
    wd:Q4009406  # 短距離走選手
    wd:Q12369333  # レスリング選手
    wd:Q13382519  # 卓球選手
    wd:Q17486376  # 射撃選手
    wd:Q4270517  # スキー選手
    wd:Q24037210  # 新体操選手
    wd:Q6665249  # 柔道選手
    wd:Q17524364  # 水球選手
    wd:Q9017214  # 空手選手
    wd:Q2309784  # 自転車競技選手
    wd:Q15972912  # 近代五種選手
    wd:Q11513337  # 陸上競技選手
  }
  ?person wdt:P106 ?occupation.

  # 性別
  OPTIONAL { ?person wdt:P21 ?gender. }

  # 生年月日
  OPTIONAL { ?person wdt:P569 ?birthDate. }
  
  # 出身地（出生地）
  OPTIONAL { ?person wdt:P19 ?birthPlace. }
  
  # 所属チーム
  OPTIONAL { ?person wdt:P54 ?team. }
  
   # ポジション
  OPTIONAL { ?person wdt:P413 ?position. }
  
  # 身長
  OPTIONAL { ?person wdt:P2048 ?height. }
  
  # 体重
  OPTIONAL { ?person wdt:P2067 ?weight. }
  
  # 出身校
  OPTIONAL { ?person wdt:P69 ?school. }
  
  # 受賞
  OPTIONAL { ?person wdt:P166 ?award. }
  
  # ラベルを日本語優先で取得
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "ja,en".
  }
}

